#compdef ldconfig ldconfig.real

local ret=1
local -a context line state state_descr
local -A opt_args

_arguments -s -S : \
  '(x -v --verbose)'{-v,--verbose}'[output verbosely]' \
  '(x)*: :->files' \
  + x \
  '(: -)'{-\?,--help}'[display help information]' \
  '(: -)'{-p,--print-cache}'[display libraries in current cache]' \
  '(: -)--usage[display brief usage information]' \
  '(: -)'{-V,--version}'[display version information]' \
  + l \
  '(x o)-l[manually link individual libraries (library mode)]' \
  + o \
  '(x l -c --format)'{-c+,--format=}'[specify cache format]:cache format:(old new compat)' \
  '(x l)-C+[specify cache file to use instead of /etc/ld.so.cache]:cache file:_files' \
  '(x l)-f+[specify configuration file to use instead of /etc/ld.so.conf]:configuration file:_files' \
  '(x l -i --ignore-aux-cache)'{-i,--ignore-aux-cache}'[ignore auxiliary cache file]' \
  '(x l -N)-n[process only directories specified on command line (implies -N)]' \
  '(x l)-N[do not rebuild cache]' \
  '(x l)-r+[specify root directory]:root directory:_directories' \
  '(x l)-X[do not update symbolic links]' \
&& ret=0

[[ $state == files ]] &&
if [[ -n ${opt_args[(I)l--l]} ]]; then
  _object_files && ret=0
else
  _directories && ret=0
fi

return ret
